MEMORY
{
    rom(rx)   : ORIGIN = 0x0,       LENGTH = 0x4000 /* 0x1000000 */
    ram_a(rw) : ORIGIN = 0x1000000, LENGTH = 0x2000 /* 0x0800000 */
    ram_b(rw) : ORIGIN = 0x1800000, LENGTH = 0x2000 /* 0x0800000 */
}
SECTIONS
{
    .boot : { *(.boot*)
        KEEP (*(.boot))
    } > rom
    .srodata : { *(.srodata*) } > rom
    .text : { *(.text*) } > rom
    .rodata : { *(.rodata*) } > rom

    .data : { 
        . = ALIGN(4);
        __data_start__ = .;
        *(.sdata*) *(.data*) 
        . = ALIGN(4);
        __data_end__ = .;
    } > ram_a AT> rom
    __etext = LOADADDR(.data);

    .bss : { 
        . = ALIGN(4);
        __bss_start__ = .;
        *(.sbss*) *(.bss*)
        . = ALIGN(4);
        __bss_end__ = .;
    } > ram_a

    .heap (NOLOAD):
    {
        __HeapStart = .;
        KEEP(*(.heap*))
        __HeapLimit = .;
    } > ram_b

    __global_pointer$ = 0x1000;
    __StackTop = ORIGIN(ram_a) + LENGTH(ram_a);
    __StackLimit = __StackTop - 0x1000;
    ASSERT(__StackLimit >= __bss_end__, "region RAM overflowed")
}
